// To publish to gradle plugins
// First, apply the publishing plugin
buildscript {
    // Variables
    ext {
        CurrentVersion = "1.0.1-SNAPSHOT"
        CurrentGroup = "com.github.pt-osda.java-dependencies-analysis"
    }

    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
    }
}

group CurrentGroup
version CurrentVersion

apply plugin: "java"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "java-gradle-plugin"

sourceCompatibility = 1.8

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.5'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.6'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'

    testCompile group: "junit", name: "junit", version: "4.12"
}

pluginBundle {
    website = "https://github.com/pt-osda/java-dependencies-analysis"
    vcsUrl = "https://github.com/pt-osda/java-dependencies-analysis"
    description = "Analyse Open Source dependencies in projects development. Aplication to analyse security and license aspects in Open Source project\'s dependencies, applicable to Java platform."
    tags = ["java", "gradle", "dependencies analysis", "License validation", "vulnerabilities"]

    plugins {
        greetingsPlugin {
            id = CurrentGroup
            displayName = "Java Dependencies Analysis"
            version = CurrentVersion
        }
    }
}