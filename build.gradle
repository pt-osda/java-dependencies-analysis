group 'com.github.pt-osda.java-dependencies-analysis'
version "1.0.0"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'java-gradle-plugin'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def doUploadArchives = project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')
if (doUploadArchives) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'OSDA - Open Source Dependencies Analysis'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Analyse Open Source dependencies in projects development. Aplication to analyse security and license aspects in Open Source project\'s dependencies, applicable to Java platform.'
                    url 'https://github.com/pt-osda/java-dependencies-analysis'

                    scm {
                        connection 'scm:git:git://github.com/pt-osda/java-dependencies-analysis.git'
                        developerConnection 'scm:git:ssh://github.com:pt-osda/java-dependencies-analysis.git'
                        url 'https://github.com/pt-osda/java-dependencies-analysis.git'
                    }

                    licenses {
                        license {
                            name 'BSD 3-Clause License'
                            url 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'RuiDTLima'
                            name 'Rui Diogo Tom√°s Lima'
                            email 'rui_lima24@hotmail.com'
                        }
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}